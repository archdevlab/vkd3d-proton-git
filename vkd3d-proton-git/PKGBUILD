#Maintainer: archdevlab <https://github.com/archdevlab>

pkgname=vkd3d-proton-git
pkgdesc='D3D12 to Vulkan translation library (git version)'
pkgver=2.13.r48.gebe7279
pkgrel=1
arch=(x86_64)
url='https://source.winehq.org/git/vkd3d.git'
license=(LGPL)
depends=(spirv-tools vulkan-icd-loader)
makedepends=(meson glslang wine xcb-util-keysyms xorgproto git)
conflicts=(vkd3d-proton)
provides=(vkd3d-proton vkd3d-proton-git)
source=(git+https://github.com/HansKristian-Work/vkd3d-proton.git)

pkgver(){
  cd "${srcdir}"/vkd3d-proton
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${srcdir}"/vkd3d-proton
  git submodule update --init --recursive
}

build(){
  rm -rf "${srcdir}"/build

  local meson_options=(
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    -D bindir=bin
    -D datadir=share
    -D includedir=include
    -D libdir=lib
    -D sysconfdir=/etc
    -D enable_tests=false
    -D enable_extras=false
    -D enable_profiling=false
    -D enable_renderdoc=false
    -D enable_descriptor_qa=false
    -D enable_trace=auto
  )

  meson "${srcdir}"/vkd3d-proton "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/vkd3d-proton/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  install -m644 "${srcdir}"/vkd3d-proton/COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  install -m644 "${srcdir}"/vkd3d-proton/AUTHORS "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
