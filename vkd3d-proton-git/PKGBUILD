# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vkd3d-proton-git
pkgdesc='D3D12 to Vulkan translation library (git version)'
pkgver=2.5.r88.gc0a3fa8a
pkgrel=1
arch=(x86_64)
url='https://source.winehq.org/git/vkd3d.git'
license=(LGPL)
depends=(spirv-tools vulkan-icd-loader libxcb)
makedepends=(git meson ninja glslang spirv-headers vulkan-headers xcb-proto wine xorgproto
             xcb-util-keysyms gcc gcc-libs)
conflicts=(vkd3d-proton)
provides=(vkd3d-proton vkd3d-proton-git)
source=(git+https://github.com/HansKristian-Work/vkd3d-proton.git)
md5sums=(SKIP)

pkgver(){
  cd ${srcdir}/vkd3d-proton

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/vkd3d-proton

  git submodule update --init --recursive
}

build(){
  cd ${srcdir}/vkd3d-proton

  rm -rf build64

  mkdir build64

  meson setup build64 \
  -D b_ndebug=true \
  -D buildtype=plain \
  --wrap-mode=nofallback \
  -Dprefix=/usr \
  -D sysconfdir=/etc \
  -Denable_tests=false \
  -Denable_extras=false \
  -Denable_profiling=false \
  -Denable_renderdoc=false \
  -Denable_descriptor_qa=false \
  -Denable_trace=auto

  #meson configure build64/
  
  ninja -C build64/

  meson compile -C build64/
}

package(){
  DESTDIR="${pkgdir}" ninja -C ${srcdir}/vkd3d-proton/build64/ install

  # installing license
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d-proton/LICENSE
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d-proton/COPYING
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d-proton/AUTHORS
}
