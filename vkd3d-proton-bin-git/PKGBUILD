# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Cyano Hao <c@cyano.cn>

pkgname=vkd3d-proton-bin-git
pkgdesc='Direct3D 12 to Vulkan translation library (Windows DLL binary files) (git version)'
pkgver=2.6.r48.gdd6534f3
pkgrel=1
url='https://github.com/HansKristian-Work/vkd3d-proton'
license=(LGPL)
arch=(x86_64)
depends=(spirv-tools vulkan-icd-loader)
makedepends=(git meson glslang wine xcb-util-keysyms xorgproto mingw-w64-gcc)
provides=(vkd3d-proton-bin vkd3d-proton-bin-git)
source=(git+https://github.com/HansKristian-Work/vkd3d-proton)
md5sums=(SKIP)
install=${pkgname}.install
options=(!buildflags)

pkgver(){
  cd ${srcdir}/vkd3d-proton

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/vkd3d-proton

  git submodule update --init --recursive
}

build(){
  cd ${srcdir}/vkd3d-proton

  rm -rf build

  mkdir build

  ./package-release.sh master build --no-package
}

package(){
  install -D "$srcdir"/vkd3d-proton/build/vkd3d-proton-master/x86/* -t "$pkgdir/usr/share/vkd3d-proton/x86"
  install -D "$srcdir"/vkd3d-proton/build/vkd3d-proton-master/x64/* -t "$pkgdir/usr/share/vkd3d-proton/x64"
  install "$srcdir"/vkd3d-proton/build/vkd3d-proton-master/setup_vkd3d_proton.sh -t "$pkgdir/usr/share/vkd3d-proton/"

  # installing license
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d-proton/LICENSE
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d-proton/COPYING
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d-proton/AUTHORS
}
